defaultTasks 'clean', 'install'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'io.github.j8spec'
version = '3.0.1-SNAPSHOT'
ext.isReleaseBuild = !version.endsWith("SNAPSHOT")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

if (isReleaseBuild) {
    ext.'signing.keyId' = System.env.J8SPEC_SIGNING_KEY_ID
    ext.'signing.secretKeyRingFile' = System.env.J8SPEC_SIGNING_KEY_RING_FILE
    ext.'signing.password' = System.env.J8SPEC_SIGNING_PASSWORD
}

repositories {
    mavenCentral()
}

dependencies {
    compile     group: 'junit',         name: 'junit',          version: '4.13.2'
    testCompile group: 'org.mockito',   name: 'mockito-core',   version: '1.10.19'
}

jar {
    manifest {
        attributes 'Sealed': 'true'
    }
}

javadoc {
    options.addStringOption "link", "http://docs.oracle.com/javase/8/docs/api/"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    doFirst {
        if (!System.env.J8SPEC_SONATYPE_USERNAME || !System.env.J8SPEC_SONATYPE_PASSWORD) {
            throw new GradleException('J8SPEC_SONATYPE_USERNAME or J8SPEC_SONATYPE_PASSWORD environment variables not defined (required for uploading artifacts).')
        }
    }

    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: System.env.J8SPEC_SONATYPE_USERNAME, password: System.env.J8SPEC_SONATYPE_PASSWORD)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: System.env.J8SPEC_SONATYPE_USERNAME, password: System.env.J8SPEC_SONATYPE_PASSWORD)
            }

            pom.project {
                name 'j8spec'
                description 'Library that allows tests written in Java to follow the BDD style introduced by RSpec and Jasmine.'

                url 'http://j8spec.github.io'

                scm {
                    url 'scm:https://github.com/j8spec/j8spec.git'
                    connection 'scm:https://github.com/j8spec/j8spec.git'
                    developerConnection 'scm:https://github.com/j8spec/j8spec.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://github.com/j8spec/j8spec/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'tprado'
                        name 'Thiago Prado'
                        email 'thiago.gozzi.prado@gmail.com'
                    }
                }
            }
        }
    }
}

install.dependsOn check, test
